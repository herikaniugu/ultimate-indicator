// Â© Kaniugu

//@version=5
indicator("Multiple Timeframe", overlay = true)

// INPUT
tf                      = input.timeframe("60", "Timeframe")
candlestick             = input.bool(false,     "Show Timeframe", group = "Visibility")

// OFFSET
minutes(n)              => n == "1D" ? 24 * 60 : n == "1W" ? 24 * 60 * 7 : (str.tonumber(n) > 0 ? str.tonumber(n) : 0)
count                   =  int(minutes(tf) / minutes(timeframe.period))
offset                  =  count < 5000 and count > 1 ? count : 0

// CANDLESTICK
HIGH                    = request.security(syminfo.tickerid, tf, high [barstate.isrealtime ? 1 : 0])
LOW                     = request.security(syminfo.tickerid, tf, low  [barstate.isrealtime ? 1 : 0])

// LEVEL
var up                  =  array.new_float(0)
var down                =  array.new_float(0)
look(arr, index)        => array.size(arr) > 3 ? array.get(arr, array.size(arr) - index) : na
if HIGH != HIGH[1]
    array.push(up,   HIGH)
if LOW  != LOW[1]
    array.push(down, LOW)

// LEVEL
var peak                = float(na)
var trough              = float(na)
peak                    := ta.crossunder(low, look(down, 1)) ? HIGH : peak
trough                  := ta.crossover(high, look(up,   1)) ? LOW  : trough

// DRAW SIGNAL
plotshape(peak   > peak[1],   style = shape.circle, color = color.red,  location = location.abovebar, size = size.tiny)
plotshape(trough < trough[1], style = shape.circle, color = color.blue, location = location.belowbar, size = size.tiny)
plotshape(peak   < peak[1],   style = shape.cross,  color = color.red,  location = location.abovebar, size = size.tiny)
plotshape(trough > trough[1], style = shape.cross,  color = color.blue, location = location.belowbar, size = size.tiny)

// DRAW TIMEFRAME
var line resistance = na, line.delete(resistance[1]), resistance := candlestick and offset > 0 ? line.new(bar_index - offset + 1, HIGH, bar_index + 2, HIGH, xloc.bar_index, extend.none, color.red,    line.style_dotted, 2) : na
var line support = na,    line.delete(support[1]),    support    := candlestick and offset > 0 ? line.new(bar_index - offset + 1, LOW,  bar_index + 2, LOW,  xloc.bar_index, extend.none, color.blue,   line.style_dotted, 2) : na
plot(candlestick and offset > 0 ? HIGH : na, offset = -offset + 1, color = color.red,  linewidth = 1)
plot(candlestick and offset > 0 ? LOW  : na, offset = -offset + 1, color = color.blue, linewidth = 1)

// SIGNAL
if peak   > peak[1]
    alert(message = "Entry Short", freq = alert.freq_once_per_bar)
if trough < trough[1]
    alert(message = "Entry Long",  freq = alert.freq_once_per_bar)